S     = 'typeInt' SR (OK) 
      | NotIntType Decl S (OK)
      | 'typeVoid' 'identifier' DeclFun S (OK)
      | EPSILON

SR  =  'funcDecl' SAux (OK) 
    | TypeArrOpc 'funcDecl' 'identifier' DeclFun S (OK)
    | 'identifier' DeclVar S (OK)

SAux = 'main' Main (OK) 
     | 'identifier' DeclFun S (OK) 

Main = '(' ')' Body  (OK)

Body = '{' Content '}'  (OK)

Content = Command Content  (OK)
        | VarType 'identifier' DeclVar Content (OK)
        | 'identifier' Contentx (OK)
        | 'funcRtn' Rtn ';' (OK)
        | EPSILON (OK)

Contentx = '(' ParamsCall ')' ';' Content (OK)
         | LAttr ';' Content (OK)        

Command = 'cmdIf' '(' Eb ')' Body LElif CmdElse (OK)
        | 'cmdWhile' '(' Eb ')' Body (OK)
        | 'cmdFor' 'identifier' '=' '(' Ea ',' Ea ',' Ea ')' Body 
        | 'funcRead' '(' LIdentifier ')' ';' (OK)
        | 'funcPrint' '(' 'stringVal' PrintParams ')' ';' (OK)

LElif   = 'cmdElif' '(' Eb ')' Body LElif (OK)
        | EPSILON (OK)

CmdElse = 'cmdElse' Body (OK)
        | EPSILON (OK)

LIdentifier = 'identifier' LIdentifierx (OK)
            | EPSILON (OK)

LIdentifierx = ',' 'identifier' LIdentifierx (OK)
            | EPSILON (OK)

PrintParams = ',' Eb PrintParams  (OK)
            | EPSILON (OK)

VarType = 'typeInt' (OK)
        | 'typeFloat' (OK)
        | 'typeString' (OK)
        | 'typeChar' (OK)
        | 'typeBool' (OK)

DeclVar = LVar ';' (OK)

LVar    = Varx LVarx  (OK)
        | EPSILON

LVarx = ',' Var LVarx (OK)
    |EPSILON (OK)

Var = 'identifier' Varx (OK)
 
Varx = '=' Ec (OK)
     | ArrOpc (OK)

ParamsCall  = Ec ParamsCallx (OK)
            | EPSILON (OK)

ParamsCallx = ',' Ec ParamsCallx (OK)
            |EPSILON (OK)

LAttr =  '=' Ec LAttrx (OK)
      |  '[' Ea ']' '=' Ec LAttrx (OK)

LAttrx = ',' LAttrxx (OK)
        |EPSILON (OK)

LAttrxx = 'identifier' '=' Ec LAttrx (OK)
         | '[' Ea ']' '=' Ec LAttrx (OK)

Rtn    = Ec   (OK)
       | EPSILON (Caso void)         

DeclFun = '(' Params ')' Body (OK)  

Params = VarType 'identifier' ArrOpc Paramsx (OK)
            | EPSILON (OK)

Paramsx = ',' VarType 'identifier' ArrOpc Paramsx (OK)
        | EPSILON (OK)            

ArrOpc = '[' ArrSizeObg ']' (OK)
        | EPSILON (OK)

ArrSizeObg = 'identifier' (OK)
            | 'intVal'     (OK)  

TypeArrOpc = '[' ']' (OK)
           | EPSILON (OK)

NotIntType  = 'typeFloat' (OK)
            | 'typeString' (OK)
            | 'typeChar' (OK)
            | 'typeBool' (OK)

Decl = TypeArrOpc Declx (OK)

Declx = 'funcDecl' 'identifier' DeclFun (OK)
      | 'identifier' DeclVar (OK)

Ec          = Eb Ecx

Ecx = 'opConcat' Eb Ecx
    | EPSILON

Eb          = Tb Ebx

Ebx = 'opOr' Tb Ebx
    | EPSILON

Tb         = Fb Tbx

Tbx = 'opAnd' Fb Tbx
    | EPSILON

Fb  = 'opNot' Fb
    | Ra Fbx

Fbx = 'opGtrThan' Fb Fbx
    | 'opLessThan' Fb Fbx
    | 'opGtrEqual' Fb Fbx
    | 'opLessEq' Fb Fbx
    | EPSILON

Ra          = Ea Rax

Rax = 'opEquals' Ea Rax
    | 'opDiff' Ea Rax
    | EPSILON              

Ea          = Ta Eax

Eax = 'opAdd' Ta Eax
    | 'opSub' Ta Eax
    | EPSILON

Ta          = Fa Tax

Tax = 'opMult' Fa Tax
    | 'opDiv' Fa Tax
    | 'opMod' Fa Tax
    | EPSILON               

Fa          = '(' Ec ')'
            | 'opSub' Fa        
            | VarOrFunc
            | 'intVal'
            | 'floatVal'
            | 'stringVal'
            | 'charVal'
            | 'boolVal'


VarOrFunc   = 'identifier' VarOrFuncx (OK) 

VarOrFuncx  = '[' Ea ']' (OK) 
            | '(' ParamsCall ')' (OK) 
            |EPSILON (OK) 
